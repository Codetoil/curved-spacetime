plugins {
    id 'java'
    id "org.jetbrains.qodana" version "2025.2.1"
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

// Maven Central - https://repo1.maven.org/maven2/
project.ext.lwjglVersion = "3.3.6" // org.lwjgl:lwjgl, org.lwjgl:lwjgl-glfw, org.lwjgl:lwjgl-vulkan
project.ext.junitVersion = "5.13.1" // org.junit:junit-bom
project.ext.tinyLoggerVersion = "2.8.0-M1" // org.tinylog:tinylog-impl
project.ext.gsonVersion = "2.13.1" // com.google.code.gson:gson
project.ext.guavaVersion = "33.4.8-jre" // com.google.guava:guava

// QuiltMC Maven - https://maven.quiltmc.org/repository/release/
project.ext.quiltLoaderVersion = "0.29.2-beta.1" // org.quiltmc:quilt-loader, org.quiltmc:quilt-loader-dependencies

// NeoForge Maven - https://maven.neoforged.net/releases
project.ext.modLauncherVersion = "11.0.5" // cpw.mods:modlauncher

static String getLWJGLNativesName() {
    String lwjglNatives = null

    if (DefaultNativePlatform.currentOperatingSystem.linux) {
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64") ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}" : "natives-linux"
    }
    if (DefaultNativePlatform.currentOperatingSystem.macOsX) {
        lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
    }
    if (DefaultNativePlatform.currentOperatingSystem.windows) {
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.contains("64") ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}" : "natives-windows-x86"
    }
    return lwjglNatives
}

subprojects { Project project ->
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
        maven {
            url = "https://maven.fabricmc.net/"
        }
        maven {
            url = "https://maven.quiltmc.org/repository/release/"
        }
        maven {
            url = "https://maven.neoforged.net/releases/"
        }
    }
}

dependencies {}