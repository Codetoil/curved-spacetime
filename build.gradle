plugins {
    id 'java'
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

group = 'io.codetoil'
version = '0.1.0-SNAPSHOT'

project.ext.asmVersion = "9.6"
project.ext.lwjglVersion = "3.3.3"
project.ext.junitVersion = "5.10.2"
project.ext.jomlVersion = "1.10.5";
project.ext.tinyLoggerVersion = "2.6.2"
project.ext.quiltLoaderVersion = "0.24.0-beta.8"
project.ext.tinyMappingsParserVersion = "0.3.0+build.17"
project.ext.tinyRemapperVersion = "0.9.0"
project.ext.accessWidenerVersion = "2.1.0"
project.ext.spongeMixinVersion = "0.12.5+mixin.0.8.5"
project.ext.gsonVersion = "2.10.1"
project.ext.guavaVersion = "33.0.0-jre"
project.ext.quiltJson5Version = "1.0.4+final"
project.ext.quiltConfigVersion = "1.2.0"
project.ext.quiltLoaderJUnitVersion = "0.24.0-beta.8"
project.ext.mixinExtrasVersion = "0.3.5"
project.ext.modLauncherVersion = "10.1.10"

static String getLWJGLNativesName()
{
    String lwjglNatives = null;

    if (DefaultNativePlatform.currentOperatingSystem.linux) {
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
    }
    if (DefaultNativePlatform.currentOperatingSystem.macOsX) {
        lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
    }
    if (DefaultNativePlatform.currentOperatingSystem.windows) {
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
    }
    return lwjglNatives;
}

subprojects {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
        maven {
            url "https://maven.fabricmc.net/"
        }
        maven {
            url "https://maven.quiltmc.org/repository/release/"
        }
        maven {
            url "https://maven.neoforged.net/releases"
        }
    }
}

dependencies {}