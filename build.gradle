plugins {
    id 'java'
    id "org.jetbrains.qodana" version "2025.2.1"
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

// Maven Central - https://repo1.maven.org/maven2/
project.ext.lwjglVersion = "3.3.6" // org.lwjgl:lwjgl, org.lwjgl:lwjgl-glfw, org.lwjgl:lwjgl-vulkan
project.ext.junitVersion = "6.0.0-RC3" // org.junit:junit-bom
project.ext.tinyLoggerVersion = "2.8.0-M1" // org.tinylog:tinylog-impl
project.ext.gsonVersion = "2.13.2" // com.google.code.gson:gson
project.ext.guavaVersion = "33.5.0-jre" // com.google.guava:guava

// FabricMC Maven - https://maven.fabricmc.net/
project.ext.fabricMixinVersion = "0.16.4+mixin.0.8.7" // net.fabricmc:sponge-mixin

// QuiltMC Maven - https://maven.quiltmc.org/repository/release/
project.ext.quiltLoaderVersion = "0.29.2-beta.5" // org.quiltmc:quilt-loader, org.quiltmc:quilt-loader-dependencies

static String getLWJGLNativesName() {
    String lwjglNatives = null

    if (DefaultNativePlatform.currentOperatingSystem.linux) {
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64") ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}" : "natives-linux"
    }
    if (DefaultNativePlatform.currentOperatingSystem.macOsX) {
        lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
    }
    if (DefaultNativePlatform.currentOperatingSystem.windows) {
        def osArch = System.getProperty("os.arch")
        lwjglNatives = osArch.contains("64") ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}" : "natives-windows-x86"
    }
    return lwjglNatives
}

subprojects { Project project ->
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    repositories {
        mavenCentral()
        maven {
            url = "https://maven.fabricmc.net/"
        }
        maven {
            url = "https://maven.quiltmc.org/repository/release/"
        }
        maven {
            url = "https://maven.neoforged.net/releases/"
        }
    }
}

dependencies {}