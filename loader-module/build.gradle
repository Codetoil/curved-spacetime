import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

import java.nio.file.Paths

plugins {
    id 'java'
}

configurations {
    dependencyScope("vulkanGLFWRenderModuleRuntimeOnly") {
        canBeConsumed = false
        canBeResolved = false
    }
}

dependencies {
    implementation project(":main-module")
    implementation "org.ow2.asm:asm:${asmVersion}"
    implementation "org.ow2.asm:asm-util:${asmVersion}"
    implementation "org.ow2.asm:asm-commons:${asmVersion}"
    implementation "org.ow2.asm:asm-analysis:${asmVersion}"
    implementation "org.ow2.asm:asm-tree:${asmVersion}"
    implementation("org.quiltmc:quilt-loader:${quiltLoaderVersion}") {
        exclude module: "annotations"
    }

    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation 'org.junit.platform:junit-platform-launcher'

    implementation "org.tinylog:tinylog-impl:${tinyLoggerVersion}"

    runtimeOnly "net.fabricmc:tiny-mappings-parser:${tinyMappingsParserVersion}"
    runtimeOnly "net.fabricmc:tiny-remapper:${tinyRemapperVersion}"
    runtimeOnly "net.fabricmc:access-widener:${accessWidenerVersion}"
    runtimeOnly("net.fabricmc:sponge-mixin:${spongeMixinVersion}") {
        exclude group: "com.google.code.gson"
        exclude group: "com.google.guava"
    }
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    runtimeOnly "cpw.mods:modlauncher:${modLauncherVersion}"
    runtimeOnly("org.quiltmc:quilt-json5:${quiltJson5Version}") {
        exclude module: "annotations"
    }
    runtimeOnly("org.quiltmc:quilt-config:${quiltConfigVersion}") {
        exclude module: "annotations"
    }
    runtimeOnly "io.github.llamalad7:mixinextras-fabric:${mixinExtrasVersion}"

    runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}"
    runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:${getLWJGLNativesName()}"
    runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${getLWJGLNativesName()}"
    runtimeOnly "org.lwjgl:lwjgl-vulkan:${lwjglVersion}"
    if (DefaultNativePlatform.currentOperatingSystem.macOsX)
    {
        runtimeOnly "org.lwjgl:lwjgl-vulkan:${lwjglVersion}:${getLWJGLNativesName()}"
    }

    vulkanGLFWRenderModuleRuntimeOnly project(":vulkan-module")
    vulkanGLFWRenderModuleRuntimeOnly project(":glfw-render-module")
    vulkanGLFWRenderModuleRuntimeOnly project(":vulkan-glfw-render-module")
}

configurations {
    // declare a resolvable configuration that is going to resolve the runtime classpath of the application
    resolvable("runtimeClasspathWithVulkanGLFWRenderModule") {
        canBeConsumed = false
        canBeDeclared = false
        extendsFrom(implementation, vulkanGLFWRenderModuleRuntimeOnly)
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('runSingle', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'io.codetoil.curved_spacetime.loader.KnotCurvedSpacetime'
    jvmArgs = ['-Dloader.gameJarPath=../main-module/build/libs/main-module.jar',
               '-Dloader.development=true',
               '-javaagent:../quilt-tweaker-agent/build/libs/quilt-tweaker-agent.jar',
               '-Dfile.encoding=UTF-8',
               '-Dsun.stdout.encoding=UTF-8',
               '-Dsun.stderr.encoding=UTF-8']
    setWorkingDir(Paths.get(getWorkingDir().parent, "runSingle").toFile().exists() ?
            Paths.get(getWorkingDir().parent, "runSingle").toFile() :
            Paths.get(getWorkingDir().parent, "runSingle").toFile().mkdirs() ?
                    Paths.get(getWorkingDir().parent, "runSingle").toFile() : null)
}

tasks.register('runWithVulkanGLFW', JavaExec) {
    classpath = configurations.runtimeClasspathWithVulkanGLFWRenderModule + sourceSets.main.runtimeClasspath

    mainClass = 'io.codetoil.curved_spacetime.loader.KnotCurvedSpacetime'
    jvmArgs = ['-Dloader.gameJarPath=../main-module/build/libs/main-module.jar',
               '-Dloader.development=true',
               '-javaagent:../quilt-tweaker-agent/build/libs/quilt-tweaker-agent.jar',
               '-Dfile.encoding=UTF-8',
               '-Dsun.stdout.encoding=UTF-8',
               '-Dsun.stderr.encoding=UTF-8']

    setWorkingDir(Paths.get(getWorkingDir().parent, "runWithVulkanGLFW").toFile().exists() ?
            Paths.get(getWorkingDir().parent, "runWithVulkanGLFW").toFile() :
            Paths.get(getWorkingDir().parent, "runWithVulkanGLFW").toFile().mkdirs() ?
                    Paths.get(getWorkingDir().parent, "runWithVulkanGLFW").toFile() : null)
}


jar {
    manifest {
        attributes(
                'Main-Class': 'io.github.codetoil.curved_spacetime.loader.KnotCurvedSpacetime'
        )
    }
}

test {
    jvmArgs '-javaagent:../quilt-tweaker-agent/build/libs/quilt-tweaker-agent.jar'
    setWorkingDir(Paths.get(getWorkingDir().parent, "test").toFile().exists() ?
            Paths.get(getWorkingDir().parent, "test").toFile() :
            Paths.get(getWorkingDir().parent, "test").toFile().mkdirs() ?
                    Paths.get(getWorkingDir().parent, "test").toFile() : null)
}

Set<Task> initQuiltweakerAgent = getTasksByName(":quilt-tweaker-agent:jar", true);

runSingle.dependsOn(initQuiltweakerAgent)
runWithVulkanGLFW.dependsOn(initQuiltweakerAgent)
test.dependsOn(initQuiltweakerAgent)